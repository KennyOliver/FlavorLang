# ==================================================
# 1
# ==================================================
serve("Hello world!");


# ==================================================
# 2
# ==================================================
const name = "Hello, " + "world!" + " :)";
const age = 25;

serve(name);
serve("Age:", age);


# ==================================================
# 3
# ==================================================
const oven_temperature = 200;

if oven_temperature > 180 {
    serve("The oven is hot!");
} elif oven_temperature == 180 {
    serve("The oven is just right!");
} else {
    serve("The oven is too cold!");
}


# ==================================================
# 4
# ==================================================
for i in 1..5 {
    serve("Mixing... Step", i);
}

for j in 10..=1 by -3 {
    serve(j);
}


# ==================================================
# 5
# ==================================================
let flour_added = 0;

while flour_added < 3 {
    serve("Adding flour...");
    flour_added = flour_added + 1;
    serve("End of iteration");
}

serve("All flour has been added!");
serve("Flour added:", flour_added);


# ==================================================
# 6
# ==================================================
create bake_cake(temperature) {
    if temperature < 180 {
        serve("Temperature is too low to bake!");
        burn("Cake burned!"); # stops function execution immediately
	} else {
        serve("Baking cake at", temperature, "degrees!");
        deliver "Cake is ready!";
	}
}

const result = bake_cake(200);
serve(result);


# ==================================================
# 7
# ==================================================
try {
    serve("This will run!");
    burn("This recipe failed!");
    serve("This won't run!");
} rescue {
    serve("Caught an error: Recipe needs improvement.");
}

try {
    int("abc");
    serve("This won't run!");
} rescue {
    serve("(Expect a) Runtime error: Can't cast string `abc` to int.");
} finish {
    serve("This always executes!");
}


# ==================================================
# 8
# ==================================================
const dessert = "cake";

check dessert {
    is "cake":
        serve("Bake the cake!");
    is "pie":
    is "cookie":
        serve("Prepare the oven!");
        break;
    else:
        serve("Dessert not on the menu.");
}


# ==================================================
# 9
# ==================================================
serve("What's your favorite dessert?");
const favorite = sample();

serve("You chose:", favorite);

# ==================================================
# 10
# ==================================================

let stop_loop = False;
let count = 10;

serve("Preparing for launch!");

while stop_loop != True {
    serve(count);
    
    if count > 0 {
        count = count - 1;
    } else {
        stop_loop = True;
    }
}

serve("Blast off!");


# ==================================================
# 11
# ==================================================
const filepath = "./tests/output.txt";

plate_file(filepath, "Freshly baked cake ready to deliver!");
garnish_file(filepath, "\nDon't forget the toppings!");

const data = taste_file(filepath);
serve("File Contents:");
serve(data);


# ==================================================
# 12
# ==================================================
# const time = 20;

# serve("Before error.");

# if time > 15 {
#     burn("Too late!", "The food got burnt!");
# }

# serve("After error?");


# ==================================================
# 13
# ==================================================
const a = 1;
serve(a + a);
serve(string(a) + string(a));

const b = "True";
const c = "False";
serve(b == c);
serve(b != c);

for _ in 1..=2 {
    serve("Enter a number:");
    let user_input = int(sample());
    let positive = user_input >= 0;
    serve("Positive?", positive);
}

serve(float("+8"));
serve(int(True));
serve(float(True));
serve(int(False));
serve(float(False));


# ==================================================
# 14
# ==================================================
# Using `sample()` to get user input
serve("Enter your favorite number:");
const favorite2 = sample();
serve("Your favorite is:", favorite2);

# Using `random()` with different argument counts
const num1 = random();              # Generates between 0.0 and 1.0
const num2 = random(5.0);           # Generates between 0.0 and 5.0
const num3 = random(10.0, 20.0);    # Generates between 10.0 and 20.0
serve("Random numbers:", num1, num2, num3);

for i in 1..=10 {
    serve(i, "\t->", random());
}

# Using `burn()` to raise an error
# burn("This is a fatal error with code:", 1001);


# ==================================================
# 15
# ==================================================
create get_year_from_unix(timestamp) {
    let temp_timestamp = timestamp;
    let year = 1970;

    let is_leap = ((year % 4 == 0) && ((year % 100 != 0) || (year % 400 == 0)));
    let seconds_in_year = 365 * 24 * 60 * 60;
    if is_leap {
        seconds_in_year = 366 * 24 * 60 * 60;
    }

    while (temp_timestamp >= seconds_in_year) {
        temp_timestamp = temp_timestamp - seconds_in_year;
        year = year + 1;

        is_leap = ((year % 4 == 0) && ((year % 100 != 0) || (year % 400 == 0)));
        seconds_in_year = 365 * 24 * 60 * 60;
        if is_leap {
            seconds_in_year = 366 * 24 * 60 * 60;
        }
    }

    deliver year;
}

serve(get_year_from_unix(get_time()));


# ==================================================
# 16
# ==================================================
create check_even_odd(num) {
    deliver num % 2 == 0 ? "Even" : "Odd";
}

for _ in 1..=5 {
    serve("Enter a number:");
    let input = int(sample());
    serve(check_even_odd(input));
}


# ==================================================
# 17
# ==================================================
create times_2(x) {
    deliver x * 2;
}

create times_3(x) {
    deliver x * 3;
}

create test(num, func_a, func_b) {
    deliver func_b(func_a(func_b(num)));
}

let calc17_result = test(10, times_2, times_3); # 180
serve(calc17_result);

# ==================================================
# 18
# ==================================================
# Initialize test array
let array = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10];
serve("Initial array:", array);

# Element Access
serve("Accessing 6th element (index 5):", array[5]);

# Append
array[^+] = 11;
serve("Array after appending 11:", array);

# Prepend
array[+^] = 0;
serve("Array after prepending 0:", array);

# Remove Last Element
serve("Removing last element:", array[^-]);
serve("Array after removing last element:", array);

# Remove First Element
serve("Removing first element:", array[-^]);
serve("Array after removing first element:", array);

# Slicing
serve("Slice from index 0 to 5:", array[0:5]);
serve("Reversed array:", array[::-1]);
serve("Slice from index 3 to end:", array[3:]);
serve("Slice from start to index 8:", array[:8]);
serve("Slice from index 3 to 8, skipping every 2nd element:", array[3:8:2]);
serve("Slice from index 8 to 3, skipping every 2nd element (reverse):", array[8:3:-2]);
serve("Slice from index 3 to start (reversed):", array[3::-1]);
serve("Slice from end towards index 8 (exclusive, reversed):", array[:8:-1]);
serve("Slice entire array, skipping every 2nd element:", array[::2]);
serve("Reverse array, skipping every 3rd element:", array[::-3]);

# Test completed
serve("Array operation tests complete!");

# Initialize const (immutable) array
const const_array = [10, 20, 30, 40, 50];
serve("Initial const array:", const_array);

# Attempt mutation
try {
    const_array[^+] = 60; # Trying to append to const array
    serve("This should not be shown; const array was mutated!");
} rescue {
    serve("Caught error: Cannot mutate a const array.");
}

# Confirm immutability
serve("Const array remains unchanged:", const_array);

# Try to remove element
try {
    const_array[^-]; # Attempting to remove the last element
    serve("This should not be shown; const array was mutated!");
} rescue {
    serve("Caught error: Cannot mutate a const array.");
}

# Final confirmation
serve("Const array is still:", const_array);

# Test completed
serve("Immutable array tests complete!");
